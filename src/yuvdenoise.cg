/*
 *  This file is part of the gputrans package
 *  Copyright (C) 2006 Gavin Hurlbut
 *
 *  gputrans is free software; you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation; either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program; if not, write to the Free Software
 *  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 */

/*HEADER---------------------------------------------------
* $Id$
*
* Copyright 2006 Gavin Hurlbut
* All rights reserved
*
*/

float3 contrast_frame( float2  coord               : TEXCOORD0,
                       uniform samplerRECT frame   : TEXUNIT0,
                       uniform float   luma_contrast,
                       uniform float   chroma_contrast) : COLOR
{
    string id = "$Id$";

    float3 value;

    value = texRECT( frame, coord ).xyz;
    value -= 0.5;
    value.x = value.x * luma_contrast;
    value.yz = value.yz * chroma_contrast;
    value += 0.5;
    value.x = clamp( value.x, 0.062745098, 0.921568627 );
    value.yz = clamp( value.yz, 0.062745098, 0.937254902 );

    return value;
}

float3 diff_frame( float2  coord               : TEXCOORD0,
                   uniform samplerRECT frameA  : TEXUNIT0,
                   uniform samplerRECT frameB  : TEXUNIT1) : COLOR
{
    string id = "$Id$";

    float3 value;

    value = texRECT( frameA, coord ).xyz - texRECT( frameB, coord ).xyz;
    value = abs(value);

    return value;
}

float thresh_diff( float2  coord               : TEXCOORD0,
                   uniform samplerRECT frame   : TEXUNIT0,
                   uniform float threshold) : COLOR
{
    string id = "$Id$";

    float3 value;
    float output;

    value = texRECT( frame, coord ).xyz;
    value -= threshold;
    value.x = step( 0.0, value.x );
    value.y = step( 0.0, value.y );
    value.z = step( 0.0, value.z );

    output = value.x + value.y + value.z;

    return output;
}

float decimate_add( float2  coord0             : TEXCOORD0,
                    float2  coord1             : TEXCOORD1,
                    float2  coord2             : TEXCOORD2,
                    float2  coord3             : TEXCOORD3,
                    uniform samplerRECT frame  : TEXUNIT0 ) : COLOR
{
    string id = "$Id$";

    float value;

    value  = texRECT(frame, coord0).x;
    value += texRECT(frame, coord1).x;
    value += texRECT(frame, coord2).x;
    value += texRECT(frame, coord3).x;
    
    return value;
}

float3 move_frame( float2   coord               : TEXCOORD0,
                   uniform samplerRECT frame    : TEXUNIT0,
                   uniform samplerRECT vector   : TEXUNIT1 ) : COLOR
{
    string id = "$Id$";

    float3 value;
    float2 newCoord;
    float2 vecCoord;
    float2 vec;
    float2 q, s;

    vecCoord = ((coord - 0.5) / 8.0) + 0.5;
    vec = texRECT(vector, vecCoord).xy;

    q = int(vec/2.0);
    s = vec - (q * 2.0);

    value  = texRECT(frame, coord+q).xyz;
    value += texRECT(frame, coord+q+s).xyz;
    value /= 2.0;
    
    return value;
}

float3 average_frame( float2 coord               : TEXCOORD0,
                      uniform samplerRECT frame  : TEXUNIT0,
                      uniform samplerRECT avg    : TEXUNIT1,
                      uniform float delay ) : COLOR
{
    string id = "$Id$";

    float3 value;
    float3 newval;

    value  = texRECT( avg, coord ).xyz;
    newval = texRECT( frame, coord ).xyz;

    value = ((value * delay) + newval) / (delay + 1);

    return value;
}

float thresholded_diff( float2 coord                 : TEXCOORD0,
                        uniform samplerRECT frameA   : TEXUNIT0,
                        uniform samplerRECT frameB   : TEXUNIT1,
                        uniform float threshold ) : COLOR
{
    string id = "$Id$";

    float value;

    value = texRECT( frameA, coord ).x - texRECT( frameB, coord ).x;
    value = abs(value);
    value = step( threshold, value ) * value;

    return value;
}

float lowpass_diff( float2 coord              : TEXCOORD0,
                    uniform samplerRECT frame : TEXUNIT0 ) : COLOR
{
    string id = "$Id$";

    float value;

    value  = texRECT( frame, coord ).x;
    value += texRECT( frame, coord + float2(-1,-1) ).x;
    value += texRECT( frame, coord + float2(-1, 0) ).x;
    value += texRECT( frame, coord + float2(-1, 1) ).x;
    value += texRECT( frame, coord + float2( 0,-1) ).x;
    value += texRECT( frame, coord + float2( 0, 1) ).x;
    value += texRECT( frame, coord + float2( 1,-1) ).x;
    value += texRECT( frame, coord + float2( 1, 0) ).x;
    value += texRECT( frame, coord + float2( 1, 1) ).x;
    value /= 9.0;

    value = clamp(4.0 * value * value, 0.0, 1.0);

    return value;
}

float3 correct_frame2( float2 coord                 : TEXCOORD0,
                       uniform samplerRECT frameA   : TEXUNIT0,
                       uniform samplerRECT frameB   : TEXUNIT1,
                       uniform float threshold ) : COLOR
{
    string id = "$Id$";

    float3 q;
    float3 value;
    float3 valA;
    float3 valB;
    float3 f1;
    float3 output;

    valA  = texRECT( frameA, coord ).xyz;
    valB  = texRECT( frameB, coord ).xyz;
    q     = abs( valA - valB );
    f1    = clamp((q - threshold) / threshold, 0.0, 1.0);

    /*
    value = (valB * (1.0-f1)) + (valA * f1);
    */
    value = lerp( valB, valA, f1 );
    output.x = ( q.x > threshold ? value.x : valB.x );
    output.y = ( q.y > threshold ? value.y : valB.y );
    output.z = ( q.z > threshold ? value.z : valB.z );

    return output;
}

float3 denoise_frame_pass2( float2 coord                : TEXCOORD0,
                            uniform samplerRECT frameA  : TEXUNIT0,
                            uniform samplerRECT frameB  : TEXUNIT1,
                            uniform float pp_threshold ) : COLOR
{
    string id = "$Id$";

    float3 d;
    float3 valA;
    float3 valB;
    float3 f1;
    float3 output;

    valA = texRECT( frameA, coord ).xyz;
    valB = texRECT( frameB, coord ).xyz;
    valB.yz = ( (valB.yz * 2.0) + valA.yz ) / 3.0;

    d       = abs( valA - valB );
    f1.x    = clamp( d.x / pp_threshold, 0.0, 1.0 );
    f1.yz   = clamp( (d.yz - pp_threshold) / pp_threshold, 0.0, 1.0 );

    /*
    output = (valA * f1) + (valB * (1.0-f1));
    */
    output = lerp(valB, valA, f1);

    return output;
}

float3 sharpen_frame( float2 coord                : TEXCOORD0,
                      uniform samplerRECT frame   : TEXUNIT0,
                      uniform float sharpen ) : COLOR
{
    string id = "$Id$";

    float3 value;
    float d;
    float m;
    float3 output;

    value = texRECT( frame, coord ).xyz;
    m  = value.x;
    m += texRECT( frame, coord + float2(1,0) ).x;
    m += texRECT( frame, coord + float2(1,1) ).x;
    m += texRECT( frame, coord + float2(0,1) ).x;
    m /= 4.0;

    d  = value.x - m;
    d *= sharpen;

    output.x = clamp( m+d, 0.062745098, 0.921568627 );
    output.yz = value.yz;

    return output;
}

float SAD( float2 coord                 : TEXCOORD0,
           uniform samplerRECT frameA   : TEXUNIT0,
           uniform samplerRECT frameB   : TEXUNIT1,
           uniform float  factor,
           uniform float2 offset ) : COLOR
{
    string id = "$Id$";

    float sad;
    float3 diff;

    sad = 0.0;
    coord = ((coord - 0.5) * factor) + 0.5;
    diff = abs( texRECT( frameA, coord ).xyz - 
                texRECT( frameB, coord + offset ).xyz );
    sad += diff.x + diff.y + diff.z;
    
    return sad;
}


float SAD_halfpel( float2 coord                 : TEXCOORD0,
                   uniform samplerRECT frameA   : TEXUNIT0,
                   uniform samplerRECT frameB   : TEXUNIT1,
                   uniform samplerRECT frameC   : TEXUNIT2,
                   uniform float  factor,
                   uniform float2 offsetA,
                   uniform float2 offsetB ) : COLOR
{
    string id = "$Id$";

    float sad;
    float3 diff;

    sad = 0.0;
    coord = ((coord - 0.5) * factor) + 0.5;
    diff = abs( ((texRECT( frameA, coord + offsetA ).xyz + 
                  texRECT( frameB, coord + offsetB ).xyz ) / 2.0 ) - 
                texRECT( frameC, coord ).xyz );
    sad += diff.x + diff.y + diff.z;
    
    return sad;
}

float4 vector_init( float2 coord                : TEXCOORD0 ) : COLOR
{
    string id = "$Id$";

    float4 zeros;

    zeros.xyzw = 0.0;
    return zeros;
}

float4 vector_update( float2 coord                  : TEXCOORD0,
                      uniform samplerRECT oldvector : TEXUNIT0,
                      uniform samplerRECT sadvector : TEXUNIT1,
                      uniform float x,
                      uniform float y ) : COLOR
{
    string id = "$Id$";

    float4 outvec;
    float4 oldvec;
    float sad;

    oldvec = texRECT( oldvector, coord );
    sad = texRECT( sadvector, coord ).x;

    if( oldvec.w == 0.0 || sad <= oldvec.z ) {
        outvec.x = x;
        outvec.y = y;
        outvec.z = sad;
        outvec.w = 1.0;
    } else {
        outvec = oldvec;
    }

    return outvec;
}

float vector_badcheck( float2 coord                 : TEXCOORD0,
                       uniform samplerRECT vector   : TEXUNIT0,
                       uniform float block_thresh ) : COLOR
{
    string id = "$Id$";

    float bad;
    float sad;

    sad = texRECT( vector, coord ).z;

    bad = ( sad > block_thresh ? 1.0 : 0.0 ); 

    return bad;
}

float4 vector_range( float2 coord                   : TEXCOORD0,
                     uniform samplerRECT vector     : TEXUNIT0,
                     uniform float2 resolution ) : COLOR
{
    string id = "$Id$";

    float4 vec;
    float2 veccoord;

    vec = texRECT( vector, coord );
    veccoord.xy = vec.xy + coord - 0.5;
    if( veccoord.x < 0 || veccoord.x > resolution.x ||
        veccoord.y < 32 || veccoord.y > resolution.y + 32 ) {
        vec.xy = 0;
    }

    return vec;
}

/*
 * vim:ts=4:sw=4:ai:et:si:sts=4
 */

